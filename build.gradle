plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
}

repositories {
    mavenCentral()
}

group 'com.codecrafters'
version '1.0-SNAPSHOT'

ext {
    junitVersion = '4.12'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.codecrafters'
    mainClass = 'com.codecrafters.Launcher'
}

javafx {
    version = '17.0.2'
    modules = [
        'javafx.base',
        'javafx.controls',
        'javafx.fxml',
        'javafx.graphics'
    ]
}

dependencies {
    implementation 'de.saxsys:mvvmfx:1.4.1'
    implementation 'org.xerial:sqlite-jdbc:3.8.11.2'
    implementation 'io.reactivex:rxjava:1.1.5'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.7.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.7.4'

    implementation 'org.apache.logging.log4j:log4j-api:2.5'
    implementation 'org.apache.logging.log4j:log4j-core:2.5'

    implementation 'org.jooq:jooq:3.8.2'
    implementation 'com.google.guava:guava:19.0'

    testImplementation "junit:junit:$junitVersion"
    testImplementation 'org.mockito:mockito-all:1.10.19'
}

tasks.run.jvmArgs += "-Dbase.dir=src/dist"

jlink {
    imageZip = project.file("${buildDir}/distributions/moodyfx-${javafx.platform.classifier}.zip")
    imageDir = file("$buildDir/moodyfx")
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'moodyfx'
        noConsole = true
        jvmArgs = ['-Dbase.dir=..']
    }
    addExtraDependencies("javafx")

/*
    targetPlatform("linux-x64") {
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk17-binaries/releases/download/jdk-2021-03-31-09-47/OpenJDK-jdk_x64_linux_hotspot_2021-03-30-23-31.tar.gz")
    }
    targetPlatform("windows-x64") {
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk17-binaries/releases/download/jdk-2021-03-31-09-47/OpenJDK-jdk_x64_windows_hotspot_2021-03-30-23-30.zip")
    }
*/
}

jlinkZip {
    group = 'distribution'
}

tasks.jlink.doLast {
    copy {
        from "src/dist"
        into "$buildDir/moodyfx/"
    }
}